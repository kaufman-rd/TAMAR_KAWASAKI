


.PROGRAM close_socket(); #106;Closing communication
	  TCP_CLOSE ret,sock_id;Normal socket closure
	  IF ret<0 THEN
		PRINT "TCP_CLOSE error ERROE=(",ret," ) ",$ERROR(ret)
		TCP_CLOSE ret1,sock_id;Forced closure of socket (shutdown) 
		IF ret1<0 THEN
		  PRINT "TCP_CLOSE error id=",sock_id
		END
	  ELSE
		PRINT "TCP_CLOSE OK id=",sock_id
		socket_stat = 0
	  END
.END
.PROGRAM decompose(); #1688752
	  $temp = $recv_buf[1]
	  xval = VAL($DECODE($temp,";",0))*resolution_x
	  .$trash = $DECODE($temp,";",1)
	  yval = -VAL($DECODE($temp,";",0))*resolution_y
	  .$trash = $DECODE($temp,";",1)
	  angle = -VAL($DECODE($temp,";",0))
	  .$trash = $DECODE($temp,";",1)
	  $resultcam = $DECODE($temp,";",0)
	  .$trash = $DECODE($temp,";",1)
	  POINT pickpos = startpos+TRANS(xval,yval)+RZ(angle)
	;
	  TYPE "---------------------------"
	  TYPE "POSITION STORED :-)"
	  TYPE "---------------------------"
	  TYPE "X-VALUE: ",xval
	  TYPE "Y-VALUE: ",yval
	  TYPE "ANGLE  : ",angle
	  TYPE "Result : ",$resultcam
	  TYPE "---------------------------"
	  flag = 1
	  LMOVE pickpos
.END
.PROGRAM main(); #0;Communication main program
	  socket_stat = 0
	  r = 0
	1000
	  
	  ip[1] = 192
	  ip[2] = 168
	  ip[3] = 0
	  ip[4] = 11
	  port = 10000
	  sock_id = 0
	  flag = 0
	  tout = 10
	  eret = 0
	  n = 0
	  resolution_x = 0.24
	  resolution_y = 0.24
	  ret = 0
	  IF socket_stat==1 THEN
		CALL close_socket
	  END
	  TWAIT 0.2
	  CALL open_socket;Connecting communication
	  WHILE sock_id>0 DO
		IF n==0 THEN
		  PULSE 8,1
		  n = 1
		END
		CALL recv;Receiving instruction data from PC
		IF flag==1 THEN
		  IF $resultcam=="P" THEN
			
			flag = 0
		  ELSE
			j = 0
			SWAIT -1002
			PULSE 8,1
		  END
		END
	  END
	  IF sock_id>0 THEN
		CALL close_socket
	  END
	  GOTO 1000
.END

.PROGRAM open_socket(); #475;Starting communication
	.er_count = 0
	connect:
		
	  TCP_CONNECT sock_id,port,ip[1],tout
	  PRINT "!!!"
	  IF sock_id<0 THEN
		IF .er_count>=5 THEN
		  PRINT "Connection with PC failed. Program is stopped."
		ELSE
		  .er_count = .er_count+1
		  PRINT "TCP_CONNECT error id=",sock_id,"  error count=",.er_count
		  GOTO connect
		END
	  ELSE
		PRINT "TCP_CONNECT OK id=",sock_id
		socket_stat = 1
	  END
.END



.PROGRAM recv(); #1848814;Communication Receiving data
	recv:
	  .num = 4
	  TCP_RECV ret,sock_id,$recv_buf[1],.num,tout,255
	  IF ret<0 THEN
		PRINT "TCP_RECV error in RECV",ret
		$recv_buf[1] = "000"
	  ELSE
		IF .num>0 THEN
		  PRINT "TCP_RECV OK  in RECV",ret
		  PRINT "RECEIVED: ",$recv_buf[1]
		  CALL decompose
		ELSE
		  $recv_buf[1] = "000"
		END
	  END
.END
